use codespan::FileMap;
use codespan::{ByteIndex, ByteSpan};

use syntax::concrete::{Declaration, Exposing, Literal, Module, Term, ReplCommand};
use syntax::parse::{LalrpopError, ParseError, Token};

grammar<'err, 'input>(errors: &'err mut Vec<ParseError>, filemap: &'input FileMap);

extern {
    type Location = ByteIndex;
    type Error = ParseError;

    enum Token<&'input str> {
        // Data
        "identifier" => Token::Ident(<&'input str>),
        "doc comment" => Token::DocComment(<&'input str>),
        "REPL command" => Token::ReplCommand(<&'input str>),
        "string literal" => Token::StringLiteral(<String>),
        "character literal" => Token::CharLiteral(<char>),
        "decimal literal" => Token::DecLiteral(<u64>),
        "float literal" => Token::FloatLiteral(<f64>),

        // Keywords
        "as" => Token::As,
        "else" => Token::Else,
        "_" => Token::Hole,
        "if" => Token::If,
        "in" => Token::In,
        "let" => Token::Let,
        "module" => Token::Module,
        "import" => Token::Import,
        "record" => Token::Record,
        "Record" => Token::RecordType,
        "then" => Token::Then,
        "Type" => Token::Type,
        "where" => Token::Where,

        // Symbols
        "\\" => Token::BSlash,
        ":" => Token::Colon,
        "," => Token::Comma,
        "." => Token::Dot,
        ".." => Token::DotDot,
        "=" => Token::Equal,
        "->" => Token::LArrow,
        "=>" => Token::LFatArrow,
        ";" => Token::Semi,

        // Delimiters
        "(" => Token::LParen,
        ")" => Token::RParen,
        "{" => Token::LBrace,
        "}" => Token::RBrace,
        "[" => Token::LBracket,
        "]" => Token::RBracket,
    }
}

pub ReplCommand: ReplCommand = {
    => ReplCommand::NoOp,
    <term: Term> => ReplCommand::Eval(Box::new(term)),
    <start: @L> <command: "REPL command"> <end: @R> =>? match command {
        "?" | "h" | "help" => Ok(ReplCommand::Help),
        "q" | "quit" => Ok(ReplCommand::Quit),
        command => {
            let span = ByteSpan::new(start, end);
            let command = String::from(command);
            Err(LalrpopError::User { error: ParseError::UnknownReplCommand { span, command} })
        },
    },
    <start: @L> <command: "REPL command"> <end: @R> <term: Term> =>? match command {
        "t" | "type" => Ok(ReplCommand::TypeOf(Box::new(term))),
        command => {
            let span = ByteSpan::new(start, end);
            let command = String::from(command);
            Err(LalrpopError::User { error: ParseError::UnknownReplCommand { span, command} })
        },
    },
    <start: @L> <command: "REPL command"> <end: @R> <ident: Ident> "=" <term: Term> =>? match command {
        "let" => Ok(ReplCommand::Let(ident, Box::new(term))),
        command => {
            let span = ByteSpan::new(start, end);
            let command = String::from(command);
            Err(LalrpopError::User { error: ParseError::UnknownReplCommand { span, command} })
        },
    },
};

pub Module: Module = {
    <_comment: "doc comment"*> "module" <name: IndexedIdent> ";" <declarations: Declaration*> => {
        Module::Valid { name, declarations }
    },
};

Declaration: Declaration = {
    <start: @L> "import" <name: IndexedIdent> <rename: ("as" <IndexedIdent>)?>
        <exposing: Exposing?> <end: @R> ";" =>
    {
        Declaration::Import { span: ByteSpan::new(start, end), name, rename, exposing }
    },
    <_comment: "doc comment"*> <name: IndexedIdent> ":" <ann: Term> ";" => {
        Declaration::Claim { name, ann }
    },
    <name: IndexedIdent> <params: AtomicLamParam*> "=" <body: Term>
        <wheres: ("where" "{" <Declaration+> "}")?> <end: @R> ";" =>
    {
        let (start, name) = name;
        let span = ByteSpan::new(start, end);
        let wheres = wheres.unwrap_or(vec![]);

        Declaration::Definition { span, name, params, body, wheres }
    },
    <start: @L> <recovered: !> <end: @R> ";" => {
        errors.push(super::errors::from_lalrpop(filemap, recovered.error));
        Declaration::Error(ByteSpan::new(start, end))
    },
};

Exposing : Exposing = {
    <start: @L> "(" ".." ")" <end: @R> => Exposing::All(ByteSpan::new(start, end)),
    <start: @L> "(" <names: (<ExposedIdent> ",")*> <last: ExposedIdent> <end: @R> => {
        let mut names = names;
        names.push(last);
        Exposing::Exact(ByteSpan::new(start, end), names)
    },
    <start: @L> <recovered: !> <end: @R> => {
        errors.push(super::errors::from_lalrpop(filemap, recovered.error));
        Exposing::Error(ByteSpan::new(start, end))
    },
};

ExposedIdent : ((ByteIndex, String), Option<(ByteIndex, String)>) = {
    <IndexedIdent> <("as" <IndexedIdent>)?>
};

pub Term: Term = {
    LamTerm,
    <x: LamTerm> ":" <t: Term> => {
        Term::Ann(Box::new(x), Box::new(t))
    },
};

LamTerm: Term = {
    PiTerm,
    <start: @L> "\\" <name: IndexedIdent> ":" <ann: PiTerm> "=>" <body: LamTerm> => {
        Term::Lam(start, vec![(vec![name], Some(Box::new(ann)))], Box::new(body))
    },
    <start: @L> "\\" <params: AtomicLamParam+> "=>" <body: LamTerm> => {
        Term::Lam(start, params, Box::new(body))
    },
    <start: @L> "if" <cond: AppTerm> "then" <if_true: AppTerm> "else" <if_false: AppTerm> => {
        Term::If(start, Box::new(cond), Box::new(if_true), Box::new(if_false))
    },
    <start: @L> "let" <declarations: Declaration+> "in" <body: LamTerm> => {
        Term::Let(start, declarations, Box::new(body))
    },
};

PiTerm: Term = {
    AppTerm,
    // Naively we would want to write the following rules:
    //
    // ```lalrpop
    // <params: ("(" <IndexedIdent+> ":" <PiTerm> ")")+> "->" <body: LamTerm> => {
    //      Term::Pi(params, Box::new(body))
    //  },
    //  <ann: AppTerm> "->" <body: LamTerm> => {
    //      Term::Arrow(Box::new(ann), Box::new(body))
    //  },
    // ```
    //
    // Alas this causes an ambiguity with the `AtomicTerm` rule. Therefore we
    // have to hack this in by reparsing the binder:
    <start: @L> <binder: AppTerm> "->" <body: LamTerm> <end: @R> =>? {
        super::reparse_pi_type_hack(ByteSpan::new(start, end), binder, body)
    },
};

AppTerm: Term = {
    AtomicTerm,
    <f: AtomicTerm> <args: AtomicTerm+> => {
        if let (&Term::Universe(s1, None), &Term::Literal(s2, Literal::Int(value))) = (&f, &args[0]) {
            Term::Universe(s1.to(s2), Some(value as u32)) // FIXME - overflow
        } else {
            Term::App(Box::new(f), args)
        }
    },
};

AtomicTerm: Term = {
    <start: @L> "(" <term: Term> ")" <end: @R> => Term::Parens(ByteSpan::new(start, end), Box::new(term)),
    <start: @L> "Type" <end: @R> => Term::Universe(ByteSpan::new(start, end), None),
    <start: @L> "_" <end: @R> => Term::Hole(ByteSpan::new(start, end)),
    <start: @L> <value: "string literal"> <end: @R> => Term::Literal(ByteSpan::new(start, end), Literal::String(value)),
    <start: @L> <value: "character literal"> <end: @R> => Term::Literal(ByteSpan::new(start, end), Literal::Char(value)),
    <start: @L> <value: "decimal literal"> <end: @R> => Term::Literal(ByteSpan::new(start, end), Literal::Int(value)),
    <start: @L> <value: "float literal"> <end: @R> => Term::Literal(ByteSpan::new(start, end), Literal::Float(value)),
    <start: @L> <ident: Ident> => Term::Var(start, ident),
    <start: @L> "Record" "{" <fields: (<RecordTypeField> ",")*> <last: RecordTypeField?> "}" <end: @R> => {
        let mut fields = fields;
        fields.extend(last);
        Term::RecordType(ByteSpan::new(start, end), fields)
    },
    <start: @L> "record" "{" <fields: (<RecordField> ",")*> <last: RecordField?> "}" <end: @R> => {
        let mut fields = fields;
        fields.extend(last);
        Term::Record(ByteSpan::new(start, end), fields)
    },
    <term: AtomicTerm> "." <label_start: @L> <label: Ident> => {
        Term::Proj(Box::new(term), label_start, label)
    },
    <start: @L> <recovered: !> <end: @R> => {
        errors.push(super::errors::from_lalrpop(filemap, recovered.error));
        Term::Error(ByteSpan::new(start, end))
    },
};

AtomicLamParam: (Vec<(ByteIndex, String)>, Option<Box<Term>>) = {
    <name: IndexedIdent> => (vec![name], None),
    "(" <names: IndexedIdent+> <ann: (":" <PiTerm>)?> ")" => (names, ann.map(Box::new)),
};

RecordTypeField: (ByteIndex, String, Box<Term>) = {
    <start: @L> <label: Ident> ":" <ty: Term> => (start, label, Box::new(ty)),
};

RecordField: (ByteIndex, String, Box<Term>) = {
    <start: @L> <label: Ident> "=" <ty: Term> => (start, label, Box::new(ty)),
};

IndexedIdent: (ByteIndex, String) = {
    <start: @L> <ident: Ident> => (start, ident),
};

Ident: String = {
    "identifier" => String::from(<>),
};
